# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tidal
pkgname=haskell-tidal
pkgver=2.0.0
pkgrel=6
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(
  ghc
  ghc-libs
  glibc
  haskell-clock
  haskell-colour
  haskell-hosc
  haskell-mwc-random
  haskell-network
  haskell-primitive
  haskell-random
  haskell-tidal-link
  haskell-vector
  supercollider
)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=(https://hackage.haskell.org/package/tidal-2.0.0/candidate/tidal-2.0.0.tar.gz)
sha512sums=('7c2cf5e1ebb66ff0d003c02a0129f1c40ad28a417cbe6a553c7ed11384005c01aa2b5a32551805346a105382bb37f8e8929fb0b17edbdaaf38453f39360005cc')
b2sums=('a34fd120fac09bf5bdc737364f6055bf5170d61446f97bb3cc166de89de387f9671bef50bed8f9d6d0e335d12c5c3464e4d7571217a7e5293bfebe518222c274')

build() {
  local configure_options=(
    -O
    --datasubdir=$pkgname
    --disable-library-vanilla
    --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname
    --dynlibdir=/usr/lib
    --enable-executable-dynamic
    --enable-shared
    --enable-tests
    --ghc-option=-fllvm
    --prefix=/usr
    --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  )

  cd $_name-$pkgver
  runhaskell Setup configure "${configure_options[@]}"
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}

